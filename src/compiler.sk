namespace GLSLX.Compiler {
  def compile(log Log, source Source, mode EmitMode) string {
    if log.hasErrors {
      return null
    }

    var apiTokens = Tokenizer.tokenize(log, Source.new("api.glsl", API))
    var sourceTokens = Tokenizer.tokenize(log, source)

    if log.hasErrors {
      return null
    }

    var global = Node.createGlobal
    var scope = Scope.new(.GLOBAL, null)

    Parser.parse(log, apiTokens, global, scope)

    for symbol in scope.symbols.values {
      if symbol.isFunction {
        for function = symbol.asFunction; function != null; function = function.previousOverload {
          function.flags |= Symbol.IS_IMPORTED
        }
      } else {
        symbol.flags |= Symbol.IS_IMPORTED
      }
    }

    Parser.parse(log, sourceTokens, global, scope)

    if log.hasErrors {
      return null
    }

    Resolver.resolve(log, global)

    if log.hasErrors {
      return null
    }

    removeImportedSymbols(global)

    return Emitter.emit(global, mode)
  }

  def removeImportedSymbols(node Node) {
    for child = node.firstChild, nextChild Node = null; child != null; child = nextChild {
      nextChild = child.nextSibling

      if (child.kind == .FUNCTION || child.kind == .STRUCT) && child.symbol.isImported {
        child.remove
      }

      else if child.kind == .VARIABLES {
        for variable = child.variablesType.nextSibling, nextVariable Node = null; variable != null; variable = nextVariable {
          nextVariable = variable.nextSibling
          assert(variable.kind == .VARIABLE)

          if variable.symbol.isImported {
            variable.remove
          }
        }

        if child.variablesType.nextSibling == null {
          child.remove
        }
      }
    }
  }
}
